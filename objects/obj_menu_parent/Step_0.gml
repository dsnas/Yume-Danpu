
if (is_array(lvl) == true)
{
	var l = lvl_curr;
	
	// Fade transition is inactive
	if (lvl_fader.stg == -1)
	{
		// Options
		if (is_array(lvl[l].option) == true)
		{
			// Movement
			if (lvl[l].option_move.act == true)
			{
				var _option_currOld = lvl[l].option_curr;
				
				// List type
				if (lvl[l].option_move.list.act == true)
				{
					lvl[l].option_curr += (fn_config_key_pressed(lvl[l].option_move.list.key[1]) - fn_config_key_pressed(lvl[l].option_move.list.key[0]));
					if (lvl[l].option_curr <= -1)
						lvl[l].option_curr = (array_length(lvl[l].option) - 1);
					if (lvl[l].option_curr >= array_length(lvl[l].option))
						lvl[l].option_curr = 0;
				}
			
				if (_option_currOld != lvl[l].option_curr)
				{
					_opt_curr = lvl[l].option_curr;
					_move_snd = lvl[l].option_move.snd;
					event_user(4);
					fn_aud_play(_move_snd, CONFIG_AUD_EMTR.MENU);
				}
			}
			var o = lvl[l].option_curr;
			_opt_curr = lvl[l].option_curr;
			
			// Confirmation
			if (lvl[l].option_confirm.act == true && fn_config_key_pressed(lvl[l].option_confirm.key) == true)
			{
				_confirm_snd = ((is_struct(lvl[l].option[o].value) == false) ? lvl[l].option_confirm.snd : -1);
				event_user(0);
				if (_confirm_snd != -1)
					fn_aud_play(_confirm_snd, CONFIG_AUD_EMTR.MENU);
			
				// Option's checkbox
				if (is_struct(lvl[l].option[o].check) == true)
					lvl[l].option[o].check.mark.act = !lvl[l].option[o].check.mark.act;
			}
			
			// Cancellation
			else if (lvl[l].option_cancel.act == true && fn_config_key_pressed(lvl[l].option_cancel.key) == true)
			{
				_cancel_snd = lvl[l].option_cancel.snd;
				event_user(1);
				if (_cancel_snd != -1)
					fn_aud_play(_cancel_snd, CONFIG_AUD_EMTR.MENU);
			}
		
			// Value cycling
			else if (lvl[l].option[o].value != -1)
			{
				for (var a = 0; a < 2; a++)
				{
					if (fn_config_key_pressed(lvl[l].option[o].value.arrow[a].key) == true && lvl[l].option[o].value.arrow[a].act == true)
					{
						_cycle_sign = (fn_config_key_pressed(lvl[l].option[o].value.arrow[1].key) - fn_config_key_pressed(lvl[l].option[o].value.arrow[0].key));
						event_user(2);
						if (global.config.access.rdcdMot.act == false)
						{
							lvl[l].option[o].value.colorVal = lvl[l].option[o].value.colorValTGT[true];
							lvl[l].option[o].value.arrow[a].alpha = lvl[l].option[o].value.arrow[a].alphaTgt[true];
							lvl[l].option[o].value.arrow[a].scale = lvl[l].option[o].value.arrow[a].scaleTgt[true];
						}
						fn_aud_play(global.player.thm[global.player.thm_curr].snd.move, CONFIG_AUD_EMTR.MENU, , , 1.5);
						break;
					}
				}
			}
		}
	}
	
	// Fade transition is active
	else if (lvl_fader.stg > -1)
	{
		lvl[lvl_curr].alpha = fn_lerp(lvl[lvl_curr].alpha, 0, lvl_fader.alpSpd);
		
		if (lvl_fader.wait_dur <= 0)
		{
			lvl[lvl_fader.tgt.lvl].alpha = fn_lerp(lvl[lvl_fader.tgt.lvl].alpha, 1, lvl_fader.alpSpd);
			if (lvl[lvl_fader.tgt.lvl].alpha >= (1 - lvl_fader.alpJump))
			{
				lvl[lvl_curr].alpha = 0;
				lvl[lvl_fader.tgt.lvl].alpha = 1;
				lvl_curr = lvl_fader.tgt.lvl;
				lvl_fader.stg = -1;
				if (lvl_fader.tgt.snd != undefined)
					fn_aud_play(lvl_fader.tgt.snd, CONFIG_AUD_EMTR.MENU);
				lvl_fader.tgt.snd = undefined;
			}
		}
		else
			lvl_fader.wait_dur -= 1;
	}
}
